plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.moizhassan.ffmpeg.ffmpegkit'

    compileSdk COMPILE_SDK_VERSION as int
    ndkVersion "22.1.7171670"

    defaultConfig {
        minSdk MIN_SDK_VERSION as int
        targetSdk TARGET_SDK_VERSION as int
        versionCode VERSION_NUMBER as int
        versionName VERSION_NAME as String
        project.archivesBaseName = "ffmpegkit"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["./src/main/libs"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            systemProperty 'enable.ffmpeg.kit.test.mode', 'true'
        }
    }
    publishing {
        singleVariant('release') {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

task javadoc(type: Javadoc) {
    title = 'FFmpegKit'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

publishing {
    publications {
        gpr(MavenPublication) {
            groupId = 'ai.memories'
            artifactId = 'ffmpeg-kit-https'
            version = '1.0.0'

            afterEvaluate {
                from components.release
            }
            
            pom {
                packaging = 'aar'
            } 
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/voyageh/ffmpeg-kit")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}

dependencies {
    api 'com.arthenica:smart-exception-java:0.2.1'
    testImplementation "androidx.test.ext:junit:1.1.5"
    testImplementation 'org.json:json:20230618'
}
